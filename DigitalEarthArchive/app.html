<html>

<head>

<meta charset="utf-8"/>        

<link rel="stylesheet" type="text/css" href="js/main.css">

<script type="text/javascript" src="js/main.js"></script>
<script type="text/javascript" src="js/WebGLCanvas.js"></script>
<script type="text/javascript" src="js/NewDataArea.js"></script>
<script type="text/javascript" src="js/SettingArea.js"></script>

<script type="text/javascript" src="js/SettingView/FileManager.js"></script>
<script type="text/javascript" src="js/SettingView/DataList.js"></script>
<script type="text/javascript" src="js/SettingView/DataEditor.js"></script>
<script type="text/javascript" src="js/SettingView/DataViewer.js"></script>
<script type="text/javascript" src="js/SettingView/CameraSetting.js"></script>
<script type="text/javascript" src="js/SettingView/KeywordFilter.js"></script>
<script type="text/javascript" src="js/SettingView/Config.js"></script>
<script type="text/javascript" src="js/SettingView/ConfigStyle.js"></script>
<script type="text/javascript" src="js/SettingView/ConfigDefaultParam.js"></script>
<script type="text/javascript" src="js/SettingView/VertexEdit.js"></script>

<script type="text/javascript" src="js/Function/GlobalFunctions.js"></script>
<script type="text/javascript" src="js/Function/NewDataManager.js"></script>
<script type="text/javascript" src="js/Function/CameraWorkManager.js"></script>
<script type="text/javascript" src="js/Function/PointIconManager.js"></script>
<script type="text/javascript" src="js/Function/TimeFilter.js"></script>
<script type="text/javascript" src="js/Function/TimeTraveler.js"></script>
<script type="text/javascript" src="js/Function/InformationBar.js"></script>
<script type="text/javascript" src="js/Function/keywordFilterManager.js"></script>
<script type="text/javascript" src="js/Function/VertexEditor.js"></script>

<script type="text/javascript" src="js/GuiComponents/NumberInputSlider.js"></script>
<script type="text/javascript" src="js/GuiComponents/DateInputSlider.js"></script>
<script type="text/javascript" src="js/GuiComponents/CheckboxInput.js"></script>
<script type="text/javascript" src="js/GuiComponents/PositionInput.js"></script>
<script type="text/javascript" src="js/GuiComponents/ColorInput.js"></script>
<script type="text/javascript" src="js/GuiComponents/TermInput.js"></script>
<script type="text/javascript" src="js/GuiComponents/ParametersInput.js"></script>
<script type="text/javascript" src="js/GuiComponents/PointIconInput.js"></script>

<script type="text/javascript" src="js/glObject/glObjectGrid.js"></script>
<script type="text/javascript" src="js/glObject/glObjectPointer.js"></script>
<script type="text/javascript" src="js/glObject/glObjectCube.js"></script>
<script type="text/javascript" src="js/glObject/glObjectArc.js"></script>
<script type="text/javascript" src="js/glObject/glObjectImage.js"></script>
<script type="text/javascript" src="js/glObject/glObjectBgImage.js"></script>
<script type="text/javascript" src="js/glObject/glObject2DImage.js"></script>
<script type="text/javascript" src="js/glObject/glObject2DButton.js"></script>
<script type="text/javascript" src="js/glObject/glObjectStraightLine.js"></script>
<script type="text/javascript" src="js/glObject/glObjectLine.js"></script>
<script type="text/javascript" src="js/glObject/glObjectShapeJson.js"></script>
<script type="text/javascript" src="js/glObject/glObjectPolygon.js"></script>
<script type="text/javascript" src="js/glObject/glObjectPlane.js"></script>

<script type="text/javascript" src="js/Utility/util.js"></script>
<script type="text/javascript" src="js/Utility/glMatrix.js"></script>
<script type="text/javascript" src="js/Utility/glUtilityFunc.js"></script>

</head>       


<!-- ////////////////////////////////////////////////////////// 
    ObjectID Shader 
//////////////////////////////////////////////////////////  -->

<script id='vertexShaderObjectMap' type="x-shader/x-vertex">
attribute vec3 aVertexPosition;
uniform mat4 uMVMatrix;
uniform mat4 uMWMatrix;
uniform mat4 uPMatrix;

void main()
{
    gl_Position = uPMatrix * uMVMatrix * uMWMatrix * vec4( aVertexPosition, 1.0 );
}
</script>
    
<script id='fragmentShaderObjectMap' type="x-shader/x-fragment">
precision mediump float;
uniform vec4 uObjectID;

void main()
{
    gl_FragColor = uObjectID;
}
</script> 


<!-- ////////////////////////////////////////////////////////// 
    Line ObjectID Shader 
//////////////////////////////////////////////////////////  -->

<script id='vertexShaderLineObjectMap' type="x-shader/x-vertex">
attribute vec3 aVertexPosition;
attribute vec4 aLinePosition;
    
uniform mat4 uMVMatrix;
uniform mat4 uMWMatrix;
uniform mat4 uPMatrix;
uniform vec4 uLineParam;    
    
void main() 
{
    vec3 sideDir = aVertexPosition - aLinePosition.xyz;
    vec3 pos = aLinePosition.xyz + uLineParam.x * sideDir;
    gl_Position = uPMatrix * uMVMatrix * uMWMatrix * vec4( pos, 1.0 );
}                                                   
</script> 
    
<script id='fragmentShaderLineObjectMap' type="x-shader/x-fragment">
precision mediump float;
uniform vec4 uObjectID;

void main()
{
    gl_FragColor = uObjectID;
}
</script> 


<!-- ////////////////////////////////////////////////////////// 
    Grid Shader
//////////////////////////////////////////////////////////  -->

<script id='vertexShaderGrid' type="x-shader/x-vertex">
attribute vec3 aVertexPosition;
uniform mat4 uMVMatrix;
uniform mat4 uMWMatrix;
uniform mat4 uPMatrix;
varying vec2 vPos2D;

void main()
{
    gl_Position = uPMatrix * uMVMatrix * uMWMatrix * vec4( aVertexPosition, 1.0 );
    vPos2D = gl_Position.xy;
}
</script>
    
<script id='fragmentShaderGrid' type="x-shader/x-fragment">
precision mediump float;
uniform vec4 uColor;
uniform vec4 uParam;
varying vec2 vPos2D;

void main()
{
    if( uParam.y > 0.5 )
    {
        float lengthFromCenter = length( vPos2D );
        if( lengthFromCenter > uParam.x * 400.0 )
        {
            discard;
        }
        gl_FragColor = vec4( uColor.xyz, 1.0 );
    }
    else
    {
        gl_FragColor = vec4( uColor.xyz, uParam.x );
    }
}
</script> 


<!-- ////////////////////////////////////////////////////////// 
    Single Color Shader 
//////////////////////////////////////////////////////////  -->

<script id='vertexShaderSingleColor' type="x-shader/x-vertex">
attribute vec3 aVertexPosition;
uniform mat4 uMVMatrix;
uniform mat4 uMWMatrix;
uniform mat4 uPMatrix;

void main()
{
    gl_Position = uPMatrix * uMVMatrix * uMWMatrix * vec4( aVertexPosition, 1.0 );
}
</script>

<script id='fragmentShaderSingleColor' type="x-shader/x-fragment">
precision mediump float;
uniform vec4 uColor;

void main()
{
    gl_FragColor = uColor;
}
</script> 


<!-- ////////////////////////////////////////////////////////// 
    Simple Lighting Shader
//////////////////////////////////////////////////////////  -->

<script id='vertexShaderLighting' type="x-shader/x-vertex">
attribute vec3 aVertexPosition;
attribute vec3 aVertexNormal;
uniform mat4 uMVMatrix;
uniform mat4 uMWMatrix;
uniform mat4 uPMatrix;
uniform mat4 uNormalMatrix;
varying vec3 vNormal;
varying vec3 vPosition;

void main()
{
    vNormal     = ( uNormalMatrix * vec4( aVertexNormal, 1.0 ) ).xyz;
    vPosition   = ( uMWMatrix * vec4( aVertexPosition, 1.0 ) ).xyz;
    gl_Position = uPMatrix * uMVMatrix * uMWMatrix * vec4( aVertexPosition, 1.0 );
}
</script> 

<script id='fragmentShaderLighting' type="x-shader/x-fragment">
precision mediump float;
varying vec3 vNormal;
varying vec3 vPosition;
uniform vec4 uColor;
uniform vec3 uLightPos;
uniform vec4 uParam;

void main()
{
    vec3 LightDirection = normalize( uLightPos - vPosition );
    vec3 Normal = normalize( vNormal );
    float distance = length( uLightPos - vPosition );
    float atte = 1.0 / ( 1.0 + 0.001 * distance + + 0.00001 * distance * distance );
    float diffuse = clamp( dot( Normal, LightDirection ), 0.1 , 1.0 );
    if( diffuse < 0.3 ) diffuse = 0.3;    
    if( atte < 0.7 ) atte = 0.7;
    vec3 Color = uColor.xyz * diffuse * atte + 0.1;
    
    float isBlinking = uParam.y;
    float time = uParam.x;
    if( isBlinking > 0.5 )
    {
        gl_FragColor = vec4( Color * ( 1.3 + 0.5 * sin( time ) ), 1.0 );
    }
    else
    {
        gl_FragColor = vec4( Color, 1.0 );
    }
}
</script> 


<!-- ////////////////////////////////////////////////////////// 
    Simple Texture Mapping
//////////////////////////////////////////////////////////  -->

<script id = 'vertexShaderImage' type = "x-shader/x-vertex" >
attribute vec3 aVertexPosition;
attribute vec2 aVertexUV;

uniform mat4 uMVMatrix;
uniform mat4 uMWMatrix;
uniform mat4 uPMatrix;

varying vec2 vTextureCoord;

void main() 
{
	vTextureCoord = aVertexUV;
	gl_Position = uPMatrix * uMVMatrix * uMWMatrix * vec4( aVertexPosition, 1.0 );
}                                                   
</script> 

<script id = 'fragmentShaderImage' type = "x-shader/x-fragment" >
precision mediump float;
uniform sampler2D uTexture;
uniform vec4 uParam;

varying vec2 vTextureCoord;

void main() 
{
    vec4 texelColor = texture2D( uTexture, vTextureCoord );
    
    float isBlinking = uParam.y;
    float time = uParam.x;
    if( isBlinking > 0.5 )
    {
        gl_FragColor = vec4( texelColor.xyz * ( 1.0 + 0.4 * sin( time ) ), texelColor.w );
    }
    else
    {
        gl_FragColor = texelColor;
    }
}                                             
</script> 



<!-- ////////////////////////////////////////////////////////// 
    Arc Shader
//////////////////////////////////////////////////////////  -->

<script id = 'vertexShaderArc' type = "x-shader/x-vertex" >
attribute vec3 aVertexPosition;

uniform mat4 uMVMatrix;
uniform mat4 uMWMatrix;
uniform mat4 uPMatrix;

void main() 
{
	gl_Position = uPMatrix * uMVMatrix * uMWMatrix * vec4( aVertexPosition, 1.0 );
}                                                   
</script> 

<script id = 'fragmentShaderArc' type = "x-shader/x-fragment" >
precision mediump float;
uniform vec4 uColor;
uniform vec4 uParam;

void main() 
{
    float isBlinking = uParam.y;
    float time = uParam.x;
    if( isBlinking > 0.5 )
    {
        gl_FragColor = vec4( uColor.xyz * ( 1.0 + 0.4 * sin( time ) ), uColor.w );
    }
    else
    {
        gl_FragColor = uColor;
    }
}                                             
</script> 



<!-- ////////////////////////////////////////////////////////// 
    Line Shader
//////////////////////////////////////////////////////////  -->

<script id = 'vertexShaderLine' type = "x-shader/x-vertex" >
attribute vec3 aVertexPosition;
attribute vec4 aLinePosition;
    
uniform mat4 uMVMatrix;
uniform mat4 uMWMatrix;
uniform mat4 uPMatrix;
uniform vec4 uLineParam;  

varying float vVertexIndex; 

void main() 
{
    vec3 sideDir = aVertexPosition - aLinePosition.xyz;
    vec3 pos = aLinePosition.xyz + uLineParam.x * sideDir;
    vVertexIndex = aLinePosition.w;
    gl_Position = uPMatrix * uMVMatrix * uMWMatrix * vec4( pos, 1.0 );
}                                                   
</script> 
    
<script id = 'fragmentShaderLine' type = "x-shader/x-fragment" >
precision mediump float;
uniform vec4 uColor;
uniform vec4 uParam;

varying float vVertexIndex;

void main() 
{
    if( vVertexIndex >= uParam.z && uParam.w < 1.0 ){
        discard;
    }

    float isBlinking = uParam.y;
    float time = uParam.x;
    if( isBlinking > 0.5 )
    {
        gl_FragColor = vec4( uColor.xyz * ( 1.0 + 0.4 * sin( time ) ), uColor.w );
    }
    else
    {
        gl_FragColor = uColor;
    }
}                                             
</script> 



<!-- ////////////////////////////////////////////////////////// 
    Polygon Shader
//////////////////////////////////////////////////////////  -->

<script id = 'vertexShaderPolygon' type = "x-shader/x-vertex" >
attribute vec3 aVertexPosition;

uniform mat4 uMVMatrix;
uniform mat4 uMWMatrix;
uniform mat4 uPMatrix;

void main() 
{
	gl_Position = uPMatrix * uMVMatrix * uMWMatrix * vec4( aVertexPosition, 1.0 );
}                                                            
</script> 
    
<script id = 'fragmentShaderPolygon' type = "x-shader/x-fragment" >
precision mediump float;
uniform vec4 uColor;
uniform vec4 uParam;
    
void main() 
{
    float isBlinking = uParam.y;
    float time = uParam.x;
    if( isBlinking > 0.5 )
    {
        gl_FragColor = vec4( uColor.xyz * ( 1.0 + 0.4 * sin( time ) ), uColor.w );
    }
    else
    {
        gl_FragColor = uColor;
    }
}                                             
</script> 



<!-- ////////////////////////////////////////////////////////// 
    Background Image Shader 
//////////////////////////////////////////////////////////  -->

<script id = 'vertexShaderBgImage' type = "x-shader/x-vertex" >
attribute vec3 aVertexPosition;
attribute vec2 aVertexUV;
   
varying vec2 vTextureCoord;
            
void main() 
{
    vTextureCoord = aVertexUV;
    gl_Position = vec4( aVertexPosition.xy, 0.999999, 1.0 );   
}                                                   
</script> 
            
<script id = 'fragmentShaderBgImage' type = "x-shader/x-fragment" >
precision mediump float;
uniform sampler2D uTexture;
uniform vec4 uParam;
uniform vec4 uColor;
varying vec2 vTextureCoord;

vec3 rgb2hsv(vec3 c)
{
    vec4 K = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
    vec4 p = mix(vec4(c.bg, K.wz), vec4(c.gb, K.xy), step(c.b, c.g));
    vec4 q = mix(vec4(p.xyw, c.r), vec4(c.r, p.yzx), step(p.x, c.r));
 
    float d = q.x - min(q.w, q.y);
    float e = 1.0e-10;
    return vec3(abs(q.z + (q.w - q.y) / (6.0 * d + e)), d / (q.x + e), q.x);
}

vec3 hsv2rgb(vec3 c)
{
    vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
    vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
    return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

void main() 
{
    vec4 texelColor = texture2D( uTexture, vTextureCoord );

    if( uParam.y < 0.5 ){
        gl_FragColor = vec4( texelColor.xyz * uParam.x, 1.0 );
    }else if( uParam.y < 1.5 ){
        vec3 hsv = rgb2hsv( uColor.xyz );
        hsv.z = texelColor.x * 3.0;
        vec3 rgb = hsv2rgb( hsv );
        gl_FragColor = vec4( rgb * uParam.x, 1.0 );
    }else{
        gl_FragColor = vec4( uColor.xyz * uParam.x, 1.0 );
    }
}                                             
</script> 


    
<!-- ////////////////////////////////////////////////////////// 
    2D Image Shader
//////////////////////////////////////////////////////////  -->

<script id = 'vertexShader2DImage' type = "x-shader/x-vertex" >
attribute vec3 aVertexPosition;
attribute vec2 aVertexUV;

uniform vec2 uScreenSize;
uniform vec2 uImagePosition;
uniform vec2 uImageSize;

varying vec2 vTextureCoord;
    
void main() 
{
    vTextureCoord = aVertexUV;
    vec2 pos = aVertexPosition.xy * uImageSize / uScreenSize + uImagePosition;
    gl_Position = vec4( pos.xy, -1.0, 1.0 );   
}                                                   
</script> 
    
<script id = 'fragmentShader2DImage' type = "x-shader/x-fragment" >
precision mediump float;
uniform sampler2D uTexture;
varying vec2 vTextureCoord;
    
void main() 
{
    vec4 texelColor = texture2D( uTexture, vTextureCoord );
    if( texelColor.w == 0.0 )
    {
        discard;
    }
    gl_FragColor = texelColor;
}                                             
</script> 


<!-- ////////////////////////////////////////////////////////// 
    HTML
//////////////////////////////////////////////////////////  -->

<body onload="appStart()">
<div id="MainArea">
    <div id="NewDataArea"></div>
    <div id="WebGLCanvas"></div>
    <div id="SettingArea"></div>
</div>
</body>

</html>